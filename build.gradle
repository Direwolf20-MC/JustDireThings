plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.moddev' version '2.0.137'
    id "me.modmuss50.mod-publish-plugin" version "1.1.0"
}

tasks.named('wrapper', Wrapper).configure {
    distributionType = Wrapper.DistributionType.BIN
}

version = mod_version
group = mod_group_id

base {
    archivesName = mod_id
}

def extraModsDir = "extra-mods"
def extraAPIDir = "extra-api"

repositories {
    mavenLocal()

    flatDir {
        name "extra-mods"
        dir file(extraModsDir)
    }

    flatDir {
        name "extra-api"
        dir file(extraAPIDir)
    }

    maven { //Patchouli && JEI
        url 'https://maven.blamejared.com'
        content {
            includeGroup "vazkii.patchouli"
            includeGroup "mezz.jei"
        }
    }

    maven { //Curios
        url = "https://maven.theillusivec4.top/"
        content {
            includeGroup "top.theillusivec4.curios"
        }
    }

    maven { // mek
        url = 'https://modmaven.dev/'
        content {
            includeGroup "mekanism"
            includeGroup "appeng"
        }
    }

    maven { // EMI
        name = "TerraformersMC"
        url = "https://maven.terraformersmc.com/"
    }

    maven { url = "https://www.cursemaven.com" }
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
        vendor.set(JvmVendorSpec.JETBRAINS)
    }
}


neoForge {
    version = project.neo_version

    parchment {
        mappingsVersion = project.parchment_mappings_version
        minecraftVersion = project.parchment_minecraft_version
    }

    runs {
        configureEach {
            systemProperty 'forge.logging.markers', 'REGISTRIES'
            logLevel = org.slf4j.event.Level.DEBUG
            gameDirectory = file("runs/${name}")
//            if (run.project.javaToolchains.launcherFor(java.toolchain).map { it.metadata.vendor }.getOrElse("").contains("JetBrains")) {
//            run.jvmArgument("-XX:+AllowEnhancedClassRedefinition")
//            }
        }

        client {
            client()
            programArguments.addAll '--username', 'Direwolf20'
        }

        client2 {
            client()
            programArguments.addAll '--username', 'Direwolf21'
        }

        server {
            server()
            programArgument '--nogui'
        }

        gameTestServer {
            type = "gameTestServer"
        }

        data {
            data()
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }
    }

    mods {
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

configurations {
    runtimeClasspath.extendsFrom localRuntime
}

dependencies {
    implementation "net.neoforged:neoforge:${neo_version}"

    compileOnly "mezz.jei:jei-${jei_mc_version}-common-api:${jei_version}"
    compileOnly "mezz.jei:jei-${jei_mc_version}-neoforge-api:${jei_version}"
    localRuntime "mezz.jei:jei-${jei_mc_version}-neoforge:${jei_version}"
    
    compileOnly "vazkii.patchouli:Patchouli:${patchouli_version}:api"
    localRuntime "vazkii.patchouli:Patchouli:${patchouli_version}"

    addLocalArtifacts('localRuntime', extraModsDir)
    addLocalArtifacts('compileOnly', extraAPIDir, "extra-api")
}

tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
            minecraft_version   : minecraft_version, minecraft_version_range: minecraft_version_range,
            neo_version         : neo_version, neo_version_range: neo_version_range,
            loader_version_range: loader_version_range,
            mod_id              : mod_id, mod_name: mod_name, mod_license: mod_license, mod_version: mod_version,
            mod_authors         : mod_authors, mod_description: mod_description,
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/neoforge.mods.toml']) {
        expand replaceProperties + [project: project]
    }
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

publishMods {
    def token = providers.environmentVariable("CURSE_TOKEN");
    dryRun = !token.isPresent()
    changelog = createChangelog()
    version = "${mod_version}"
    type = STABLE

    curseforge {
        projectId = "1002348"
        accessToken = token.getOrElse("")
        file = project.provider { tasks.jar }.flatMap { it.archiveFile }
        displayName = "JustDireThings-${mod_version}"
        modLoaders.add("neoforge")
        minecraftVersions.add("${minecraft_version}")
        javaVersions.add(JavaVersion.VERSION_21)
    }
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}

def addLocalArtifacts(type, dir, prefix = "extra-mods") {
    for (extraModJar in fileTree(dir: dir, include: '*.jar')) {
        def basename = extraModJar.name.substring(0, extraModJar.name.length() - ".jar".length())
        def versionSep = basename.lastIndexOf('-')
        assert versionSep != -1
        def artifactId = basename.substring(0, versionSep)
        def version = basename.substring(versionSep + 1)
        type "${prefix}:$artifactId:$version"
    }
}

def createChangelog() {
    def issueNumberPattern = ~/(?i)(?<!\S)#[0-9]+(?!\S)/
    def githubMentionPattern = ~/(?i)(?<!\S)@([a-zA-Z0-9-_]+)(?!\S)/

    def github = "https://github.com"
    def repo = "$github/direwolf20-mc/justdirethings"

    def changelogFile = file('CHANGELOG.md')
    if (!changelogFile.exists()) {
        throw new GradleException("CHANGELOG.md file not found!")
    }

    def changelogContent = changelogFile.text
    def output = ""

    def reading = false
    def lines = changelogContent.readLines()
    for (line in lines) {
        if (line.startsWith("## ") && line.contains(mod_version)) {
            println("Found changelog for version ${mod_version}")
            reading = true
            continue
        } else if (line.startsWith("## ") && reading) {
            break
        } else if (!reading) {
            continue
        }

        // Inject issues and mentions links
        line = line.replaceAll(issueNumberPattern) { fullMatch ->
            def issueNumber = fullMatch.substring(1)
            "[#${issueNumber}](${repo}/issues/${issueNumber})"
        }

        line = line.replaceAll(githubMentionPattern) { fullMatch, username ->
            "[@${username}](${github}/${username})"
        }

        output += line + "\n"
    }

    def finalOutput = output.trim();
    return finalOutput.isEmpty() ? "No changelog available." : finalOutput
}
